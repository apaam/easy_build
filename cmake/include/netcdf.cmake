message(STATUS "Configuring netcdf ...")

if(NETCDF_INCLUDED)
  return()
endif()
set(NETCDF_INCLUDED TRUE)

include(${CMAKE_SOURCE_DIR}/cmake/include/hdf5.cmake)

if(USE_INTERNAL_NETCDF)
  set(NETCDF_EP_ROOT ${CMAKE_SOURCE_DIR}/contrib/netcdf/ep)
  set(NETCDF_SOURCE_DIR ${CMAKE_SOURCE_DIR}/contrib/netcdf/src)
  set(NETCDF_BUILD_DIR ${CMAKE_SOURCE_DIR}/contrib/netcdf/build)
  set(NETCDF_INSTALL_DIR ${CMAKE_SOURCE_DIR}/contrib/netcdf/install)

  if(NOT EXISTS "${NETCDF_SOURCE_DIR}/CMakeLists.txt")
    message(SEND_ERROR "Submodule netcdf missing. To fix, try run: "
                       "git submodule update --init")
  endif()

  ExternalProject_Add(
    NETCDF
    PREFIX ${NETCDF_EP_ROOT}
    SOURCE_DIR ${NETCDF_SOURCE_DIR}
    BINARY_DIR ${NETCDF_BUILD_DIR}
    INSTALL_DIR ${NETCDF_INSTALL_DIR}
    LOG_CONFIGURE TRUE
    LOG_BUILD TRUE
    LOG_OUTPUT_ON_FAILURE TRUE
    CONFIGURE_COMMAND
      cmake ${CMAKE_GENERATOR_FLAG} -DCMAKE_INSTALL_PREFIX=${NETCDF_INSTALL_DIR}
      -DCMAKE_CXX_COMPILER=${MPICXX_COMPILER}
      -DCMAKE_C_COMPILER=${MPICC_COMPILER} -DENABLE_PARALLEL4=OFF
      -DENABLE_NETCDF_4=OFF -DBUILD_STATIC_LIBS=ON -DBUILD_SHARED_LIBS=OFF
      -DENABLE_V2_API=OFF -DENABLE_FSYNC=OFF -DENABLE_DAP=OFF
      -DHDF5_C_LIBRARY=${HDF5_C_LIBRARY} -DHDF5_HL_LIBRARY=${HDF5_HL_LIBRARY}
      -DHDF5_INCLUDE_DIR=${HDF5_INCLUDE_DIRS} ${NETCDF_SOURCE_DIR}
    BUILD_COMMAND ${GENERATOR} -j${NUM_CORES}
    INSTALL_COMMAND ${GENERATOR} -j${NUM_CORES} install)

  if(USE_INTERNAL_HDF5)
    add_dependencies(NETCDF HDF5)
  endif(USE_INTERNAL_HDF5)

  set(NETCDF_INCLUDE_DIRS ${NETCDF_INSTALL_DIR}/include)
  set(NETCDF_LIBRARIES netcdf)
  set(NETCDF_LIBRARY_DIRS ${NETCDF_INSTALL_DIR}/lib)
else()
  find_package(NETCDF)
  if(NOT NETCDF_FOUND)
    message(SEND_ERROR "Can't find system netcdf package.")
  endif()
endif()

set(NETCDF_LIBRARIES ${NETCDF_LIBRARIES} ${HDF5_LIBRARIES})
include_directories(AFTER ${NETCDF_INCLUDE_DIRS})
link_directories(AFTER ${NETCDF_LIBRARY_DIRS})
message(STATUS "Using NETCDF_INCLUDE_DIRS=${NETCDF_INCLUDE_DIRS}")
message(STATUS "Using NETCDF_LIBRARIES=${NETCDF_LIBRARIES}")
message(STATUS "Using NETCDF_LIBRARY_DIRS=${NETCDF_LIBRARY_DIRS}")
